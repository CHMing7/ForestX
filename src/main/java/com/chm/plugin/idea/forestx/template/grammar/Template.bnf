{
  parserClass="com.chm.plugin.idea.forestx.template.grammar.TemplateParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="ForestTemplate"
  psiImplClassSuffix="Impl"
  psiPackage="com.chm.plugin.idea.forestx.template.psi"
  psiImplPackage="com.chm.plugin.idea.forestx.template.psi.impl"

  elementTypeHolderClass="com.chm.plugin.idea.forestx.template.psi.TemplateTypes"
  elementTypeClass="com.chm.plugin.idea.forestx.template.psi.ForestTemplateElementType"
  tokenTypeClass="com.chm.plugin.idea.forestx.template.psi.ForestTemplateTokenType"
}

// 字符串模板 (文法总入口)
StringTemplate ::= FT_DQ FT_JSTRING? StringBlockContent* FT_DQ

// 字符串模板的代码块
StringBlockContent ::= (PropertyBlock | ElBlock) FT_JSTRING?

// 配置属性代码块
PropertyBlock ::= PROP_BLOCK_BEGIN PROP_REFERENCE PROP_BLOCK_END


// EL表达式代码块
ElBlock ::= EL_BLOCK_BEGIN ElExpress EL_BLOCK_END

// EL表达式
ElExpress ::= PathExpress

// 链路表达式
PathExpress ::= Primary (PathElement)*

// 链路元素: 点、名字、参数
PathElement ::= EL_DOT NamePart | Arguments

// 名字部分
NamePart ::= Identifier

// 参数
Arguments ::= EL_LPAREN ArgumentList? EL_RPAREN

// 参数列表
ArgumentList ::= ArgumentListElement (EL_COMMA ArgumentListElement)?

// 参数列表元素
ArgumentListElement ::= ElExpress

// 基本计算要素
Primary ::= Identifier | Integer | Decimal

// 整数
Integer ::= EL_INT

// 数值
Decimal ::= EL_DECIMAL

// EL中的标识符
Identifier ::= EL_IDENTIFIER
